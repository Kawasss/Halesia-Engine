#version 460
#define MAX_LIGHT_INDICES 7

#include "include/light.glsl"

layout (local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

struct Cell
{
	int lightCount;
	float lightIndices[MAX_LIGHT_INDICES]; // this uses floats because the padding for ints is messed up
};

layout (std430, set = 0, binding = 0) buffer Cells
{
	uint width;
	uint height;
	Cell data[];
} cells;

layout (set = 0, binding = 1) readonly buffer Lights
{
	Light data[];
} lights;

layout (set = 0, binding = 2) uniform Matrices
{
	mat4 proj;
	mat4 view;
} matrices;

// attenuation: f(x) = 1 / x ^ 2, where attenuation determines the radius of a light
#define MAX_ATTENUATION_FAST    10.0          // attenuation = 0.01
#define MAX_ATTENUATION_MEDIUM  31.6227766017 // attenuation = 0.001
#define MAX_ATTENUATION_PRECISE 100.0         // attenuation = 0.0001

vec2 GetScreenSpace(vec3 pos)
{
	vec4 screenSpace = matrices.proj * matrices.view * vec4(pos, 1);
	screenSpace.xy   = (screenSpace.xy / screenSpace.w + 1) * 0.5f;

	return screenSpace.xy;
}

vec2 ConvertScreenSpaceToCellSpace(vec2 screenSpace)
{
	return screenSpace.xy * vec2(cells.width, cells.height);
}

void main()
{
	vec3 pos = lights.data[gl_GlobalInvocationID.x].pos.xyz;
	vec2 screenSpace = GetScreenSpace(pos);
	vec2 extentScreen = GetScreenSpace(pos + vec3(MAX_ATTENUATION_MEDIUM, 0, 0));

	vec2 cellSpace = ConvertScreenSpaceToCellSpace(screenSpace);
	vec2 maxReach  = ConvertScreenSpaceToCellSpace(extentScreen);

	float reach = abs(maxReach.x - cellSpace.x);

	ivec2 minimum = ivec2(cellSpace.xy) - ivec2(reach);
	ivec2 maximum = ivec2(cellSpace.xy) + ivec2(reach);

	minimum.x = clamp(minimum.x, 0, int(cells.width)  - 1);
	minimum.y = clamp(minimum.y, 0, int(cells.height) - 1);

	maximum.x = clamp(maximum.x, 0, int(cells.width));
	maximum.y = clamp(maximum.y, 0, int(cells.height));

	for (int x = minimum.x; x < maximum.x; x++)
	{
		for (int y = minimum.y; y < maximum.y; y++)
		{
			// the circle equation for the light is (x - cellSpace.x)^2 + (y - cellSpace.y)^2 = radius^2.
			// the radius is needed, so the equation is rewritten into radius = sqrt( (x - cellSpace.x)^2 + (y - cellSpace.y)^2 ).
			// the point (x, y) is only inside the lights radius if the result is smaller than the radius thats calculated earlier.
			float dist = sqrt(pow(x + 0.5f - cellSpace.x, 2) + pow(y + 0.5f - cellSpace.y, 2));

			if (floor(dist) > reach)
				continue;

			uint index = cells.height * x + y;

			if (index >= cells.width * cells.height)
				continue; // out of bounds failsafe, just in case

			int lightCountIndex = min(cells.data[index].lightCount, MAX_LIGHT_INDICES);

			atomicAdd(cells.data[index].lightCount, 1); // this ++ is atomic, because it otherwise creates a race condition if multiple lights affect the same cell

			cells.data[index].lightIndices[lightCountIndex] = float(gl_GlobalInvocationID.x);
		}
	}
}